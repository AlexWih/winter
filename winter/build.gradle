apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile 'junit:junit:4.12'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from dokka
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask(tasks.uploadArchives) }
    sign configurations.archives
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    includes = ['dokka-packages.md']

    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/beyama/winter/blob/master/winter/src/main/kotlin"
        suffix = "#L"
    }
}

if (hasProperty('ossrhUsername') && hasProperty('ossrhPassword')) {
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'Winter'
                    packaging 'jar'
                    artifactId 'winter'
                    description 'Kotlin Dependency Injection'
                    url 'https://github.com/beyama/winter'

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'ajentz'
                            name 'Alexander Jentz'
                            email 'oss@jentz.io'
                        }
                    }

                    scm {
                        url 'https://github.com/beyama/winter'
                        connection 'scm:git:https://github.com/beyama/winter.git'
                        developerConnection 'scm:git:git@github.com:beyama/winter.git'
                        tag 'HEAD'
                    }

                    issueManagement {
                        system 'GitHub Issues'
                        url 'https://github.com/beyama/winter/issues'
                    }
                }
            }
        }
    }
}