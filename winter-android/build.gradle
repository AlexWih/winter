apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka-android'

version = VERSION_NAME
group = GROUP

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        minSdkVersion 16
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

task androidJavadocJar(type: Jar) {
    classifier = 'javadoc'
    from dokka
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

artifacts {
    archives androidSourcesJar, androidJavadocJar
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    includes = ['dokka-packages.md']

    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/beyama/winter/blob/master/winter-android/src/main/kotlin"
        suffix = "#L"
    }
}

dependencies {
    api project(":winter")

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.vintage:junit-vintage-engine:$junitVersion"
    testImplementation "io.kotlintest:kotlintest-assertions:3.1.10"
    testImplementation "org.mockito:mockito-core:2.19.0"
    testImplementation "com.nhaarman:mockito-kotlin:1.5.0"
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')